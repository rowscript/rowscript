type Option<T> = [Ok: T | None];

type Result<T, E> = [Ok: T | Err: E];

function isOk<'a, T>(e: ['a]): boolean
where (Ok: T) keyof 'a
{
    switch (e) {
    case Ok(v): true
    case v: false
    }
}

function unwrap<'a, T>(e: ['a]): T
where (Ok: T) keyof 'a
{
    switch (e) {
    case Ok(v): v
    case v: panic("unwrap error")
    }
}

interface Iterator {
    next(it: Iterator): Option<Iterator::Item>;
}

interface Iterable {
    iter<T>(a: Iterable): T where T instanceof Iterator;
}

class ArrayIterator<T> {
    type Item = T;

    NativeArrayIterator<T>;

    next(): Option<T> {
        arrayIter#next(this)
    }
}

class Array<T> {
    type Key = number;
    type Value = T;

    NativeArray<T>;

    length(): number {
        array#length(this)
    }

    push(value: T): number {
        array#push(this, value)
    }

    forEach(f: (value: T) => unit) {
        array#forEach(this, f)
    }

    at(index: number): Option<T> {
        array#at(this, index)
    }

    get(index: number): Option<T> {
        this.at(index)
    }

    __getitem__(index: number): T {
        this.get(index).unwrap()
    }

    set(index: number, value: T) {
        array#set(this, index, value)
    }

    __setitem__(index: number, value: T) {
        this.set(index, value)
    }

    iter(): ArrayIterator<T> {
        new ArrayIterator(array#iter(this))
    }
}

type Entry<K, V> = {
    key: K,
    value: V
};

class MapIterator<K, V> {
    type Item = Entry<K, V>;

    NativeMapIterator<Item>;

    next(): Option<Item> {
        mapIter#next(this)
    }
}

class Map<K, V> {
    type Key = K;
    type Value = V;

    NativeMap<K, V>;

    has(key: K): boolean {
        map#has(this, key)
    }

    get(key: K): Option<V> {
        (this.has(key)) ? Ok(map#get(this, key)) : None
    }

    __getitem__(key: K): V {
        this.get(key).unwrap()
    }

    set(key: K, value: V): Map<K, V> {
        map#set(this, key, value);
        this
    }

    __setitem__(key: K, value: V) {
        _ = this.set(key, value);
    }

    delete(key: K): boolean {
        map#delete(this, key)
    }

    clear() {
        map#clear(this)
    }

    iter(): MapIterator<K, V> {
        new MapIterator(map#iter(this))
    }
}

// Effects.

type Executor<T> = (resolve: (value: T) => unit) => unit;

interface Async {
    async __await__<T>(executor: Executor<T>): T;

    async __await_mul__<Args>(...: Args): Args;

    async __await_all__<T>(executors: NativeArray<Executor<T>>): NativeArray<T>;

    async __await_any__<T>(executors: NativeArray<Executor<T>>): T;
}

class Awaits {
    empty: unit;

    async all<T>(executors: Array<Executor<T>>): Array<T> {
        new Array(await#all(executors))
    }

    async any<T>(executors: Array<Executor<T>>): T {
        await#any(executors)
    }
}

const awaits = new Awaits(());
