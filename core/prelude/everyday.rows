function panic<T>(msg: string): T {
    error#throw(msg)
}

type Option<T> = [Ok: T | None];

type Result<T, E> = [Ok: T | Err: E];

function isOk<'A, T>(e: ['A]): boolean
where (Ok: T) keyof 'A
{
    switch (e) {
    case Ok(v): true
    case v: false
    }
}

function unwrap<'A, T>(e: ['A]): T
where (Ok: T) keyof 'A
{
    switch (e) {
    case Ok(v): v
    case v: panic("unwrap error")
    }
}

interface Iterator {
    next(it: Iterator): Option<Iterator::Item>;
}

interface Iterable {
    iter<T>(a: Iterable): T where Iterator<T>;
}

class ArrayIterator<T> {
    type Item = T;

    it: NativeArrayIterator<T>;

    next(): Option<T> {
        arrayIter#next(this.it)
    }
}

class Array<T> {
    type Key = number;
    type Value = T;

    a: NativeArray<T>;

    length(): number {
        array#length(this.a)
    }

    push(value: T): number {
        array#push(this.a, value)
    }

    forEach(f: (value: T) -> unit) {
        array#forEach(this.a, f)
    }

    at(index: number): Option<T> {
        array#at(this.a, index)
    }

    get(index: number): Option<T> {
        this.at(index)
    }

    __getitem__(index: number): T {
        this.get(index).unwrap()
    }

    set(index: number, value: T) {
        array#set(this.a, index, value)
    }

    __setitem__(index: number, value: T) {
        this.set(index, value)
    }

    iter(): ArrayIterator<T> {
        new ArrayIterator(array#iter(this.a))
    }
}

type Entry<K, V> = {
    key: K,
    value: V
};

class MapIterator<K, V> {
    type Item = Entry<K, V>;

    it: NativeMapIterator<Item>;

    next(): Option<Item> {
        mapIter#next(this.it)
    }
}

class Map<K, V> {
    type Key = K;
    type Value = V;

    m: NativeMap<K, V>;

    has(key: K): boolean {
        map#has(this.m, key)
    }

    get(key: K): Option<V> {
        (this.has(key)) ? Ok(map#get(this.m, key)) : None
    }

    __getitem__(key: K): V {
        this.get(key).unwrap()
    }

    set(key: K, value: V): Map<K, V> {
        map#set(this.m, key, value);
        this
    }

    __setitem__(key: K, value: V) {
        _ = this.set(key, value);
    }

    delete(key: K): boolean {
        map#delete(this.m, key)
    }

    clear() {
        map#clear(this.m)
    }

    iter(): MapIterator<K, V> {
        new MapIterator(map#iter(this.m))
    }
}

// Effects.

interface Await {
    __await__<T>(task: (resolve: (v: T) -> unit) -> unit): T;
}

interface AwaitAll {
    __await_all__<T>(tasks: Array<(resolve: (v: T) -> unit) -> unit>): Array<T>;
}

interface AwaitAny {
    __await_any__<T>(tasks: Array<(resolve: (v: T) -> unit) -> unit>): T;
}

interface AwaitRace {
    __await_race__<T>(tasks: Array<(resolve: (v: T) -> unit) -> unit>): T;
}
